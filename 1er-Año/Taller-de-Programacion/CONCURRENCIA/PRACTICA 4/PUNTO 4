programa punto4
procesos
  proceso recogerFlores
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
  fin
  
  proceso recogerPapeles
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
  fin
  
  proceso vaciarBolsa
  comenzar
    mientras HayFlorEnLaBolsa
      depositarFlor
    mientras HayPapelEnLaBolsa
      depositarPapel
  fin
  
areas
  area1 : AreaP(2,1,2,1)
  area2 : AreaP(3,1,3,1)
  cuadrante: AreaPC(2,2,100,100)
  areaJefe:  AreaP(1,1,1,1)
robots
  robot tipoJefe
  variables  
    id,tarea,av,ca: numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    repetir 10
      Random(av,2,100)
      Random(ca,2,100)
      Random(tarea,1,3)
      Random(id,1,2)
      si (id = 1)
        EnviarMensaje(tarea,r1)
        EnviarMensaje(av,r1)
        EnviarMensaje(ca,r1)
      sino 
        EnviarMensaje(tarea,r2)
        EnviarMensaje(av,r2)
        EnviarMensaje(ca,r2)
    EnviarMensaje(4,r1)
    EnviarMensaje(4,r2)
  fin
  
  robot trabajador
  variables
    id,tarea,av,ca,avInicial,caInicial: numero
  comenzar
    avInicial := PosAv
    caInicial := PosCa
    RecibirMensaje(id,jefe)
    RecibirMensaje(tarea,jefe)
    mientras (tarea <> 4)
      RecibirMensaje(av,jefe)
      RecibirMensaje(ca,jefe)
      BloquearEsquina(av,ca)
      Pos(av,ca)
      si (tarea = 1)
        recogerFlores
      sino 
        si (tarea = 2)
          recogerPapeles
        sino
          vaciarBolsa
      Pos(avInicial,caInicial)
      LiberarEsquina(av,ca)
      RecibirMensaje(tarea,jefe)
  fin

variables
  jefe : tipoJefe
  r1 : trabajador
  r2: trabajador
comenzar
  AsignarArea(jefe,areaJefe)
  AsignarArea(r1,area1)
  AsignarArea(r2,area2)
  AsignarArea(r2,cuadrante)
  AsignarArea(r1,cuadrante)
  
  Iniciar(jefe,1,1)
  Iniciar(r1,2,1)
  Iniciar(r2,3,1)
fin
