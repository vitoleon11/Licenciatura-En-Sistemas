programa parcialTurnoN
procesos
  proceso depositarPapeles
  comenzar
    mientras HayPapelEnLaBolsa
      depositarPapel
  fin
  proceso izquierda
  comenzar
    repetir 3
      derecha
  fin
  proceso juntarPapeles(ES p : numero)
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
      p := p + 1
  fin
  proceso hacerEscalones(E alto:numero;E ancho : numero;ES papeles:numero)
  comenzar
    repetir 3
      repetir alto
        juntarPapeles(papeles)
        mover
      derecha
      repetir ancho
        juntarPapeles(papeles)
        mover
      izquierda
  fin
areas
  area1: AreaP(2,1,11,13)
  area2: AreaP(32,1,41,13)
  area3: AreaP(62,1,71,13)
  areaFisca: AreaP(1,1,1,1)
  area100: AreaPC(100,100,100,100)
robots
  robot papelero
  variables
    id,alto,ancho,cantPapeles,avInicial,caInicial:numero
  comenzar
    avInicial := PosAv
    caInicial:= PosCa
    RecibirMensaje(id,fisca)
    RecibirMensaje(alto,fisca)
    RecibirMensaje(ancho,fisca)
    cantPapeles:= 0
    hacerEscalones(alto,ancho,cantPapeles)
    EnviarMensaje(id,fisca)
    EnviarMensaje(cantPapeles,fisca)
    BloquearEsquina(100,100)
    Pos(100,100)
    depositarPapeles
    Pos(avInicial,caInicial)
    LiberarEsquina(100,100)
  fin
  
  
  robot fiscalizador
  variables  
    alto,ancho,id,cantMaxPapeles,maxRobot,cantPapeles:numero
  comenzar
    cantMaxPapeles:= -1
    EnviarMensaje(1,P1)
    EnviarMensaje(2,P2)
    EnviarMensaje(3,P3)
    Random(alto,1,4)
    Random(ancho,1,3)
    EnviarMensaje(alto,P1)
    EnviarMensaje(alto,P2)
    EnviarMensaje(alto,P3)
    EnviarMensaje(ancho,P1)
    EnviarMensaje(ancho,P2)
    EnviarMensaje(ancho,P3)
    repetir 3
      RecibirMensaje(id,*)
      si (id = 1)
        RecibirMensaje(cantPapeles,P1)
      sino 
        si (id = 2)
          RecibirMensaje(cantPapeles,P2)
        sino
          RecibirMensaje(cantPapeles,P3)
      si (cantPapeles > cantMaxPapeles)
        cantMaxPapeles := cantPapeles
        maxRobot := id
    Informar(maxRobot)
  fin
  
  
variables
  P1: papelero
  P2: papelero
  P3: papelero
  fisca:fiscalizador
comenzar
  AsignarArea(P1,area1)
  AsignarArea(P2,area2)
  AsignarArea(P3,area3)
  AsignarArea(fisca,areaFisca)
  
  AsignarArea(P1,area100)
  AsignarArea(P2,area100)
  AsignarArea(P3,area100)
  
  Iniciar(P1,2,1)
  Iniciar(P2,32,1)
  Iniciar(P3,62,1)
  Iniciar(fisca,1,1)
fin
