programa ejemplo
procesos
  proceso izquierda
  comenzar
    repetir 3
      derecha
  fin
  
  proceso calcularFloresYPapeles(ES flores:numero;ES papeles:numero)
  comenzar
    flores := 0
    papeles := 0
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores := flores + 1
    mientras HayPapelEnLaEsquina
      tomarPapel
      papeles := papeles + 1
    mientras HayFlorEnLaBolsa
      depositarFlor
    mientras HayPapelEnLaBolsa
      depositarPapel
  fin
  
  proceso hacerEscalera(E alto: numero;ES escalones:numero)
  variables
    floresEscalon,papelesEscalon,fEsquina,pEsquina:numero
  comenzar
    repetir 4
      floresEscalon := 0
      papelesEscalon := 0
      repetir alto
        calcularFloresYPapeles(fEsquina,pEsquina)
        floresEscalon := floresEscalon + fEsquina
        papelesEscalon := papelesEscalon + pEsquina
        mover
      derecha
      calcularFloresYPapeles(fEsquina,pEsquina)
      floresEscalon := floresEscalon + fEsquina
      papelesEscalon := papelesEscalon + pEsquina
      mover
      si (floresEscalon > papelesEscalon)
        escalones := escalones +1
      izquierda
  fin
  
areas
  ciudad: AreaC (1,1,100,100)
  
robots
  robot tipo1
  variables
    alto,escalones:numero
  comenzar
    Random(alto,1,5)
    escalones := 0
    hacerEscalera(alto,escalones)
    EnviarMensaje(escalones,jefe)
  fin
  
  robot tipoJefe
  variables
    escalones1,escalones2,escalones3:numero
  comenzar
    RecibirMensaje(escalones1,robot1)
    RecibirMensaje(escalones2,robot2)
    RecibirMensaje(escalones3,robot3)
    Informar(escalones1 + escalones2 + escalones3)
  fin
  
variables
  robot1: tipo1
  robot2:tipo1
  robot3:tipo1
  jefe:tipoJefe

comenzar
  AsignarArea(jefe, ciudad)
  AsignarArea(robot1, ciudad)
  AsignarArea(robot2, ciudad)
  AsignarArea(robot3, ciudad)
  Iniciar(jefe, 1,1)
  Iniciar(robot1, 2,1)
  Iniciar(robot2, 7,1)
  Iniciar(robot3, 12,1)
fin
